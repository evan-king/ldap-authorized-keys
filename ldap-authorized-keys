#!/usr/bin/perl
use strict;
use warnings;

use File::Basename;
use Config::General;
use Net::LDAP;

my $name = basename($0);
my $PATH = '/etc/ldap.conf';
my $uid = $ARGV[0];

die "Missing config file $PATH\n" unless -e $PATH;
die "Usage: $name user\n" unless $uid;
die "No such user $uid\n" if system("getent passwd $uid > /dev/null");

my %conf = Config::General->new('/etc/ldap.conf')->getall();

my $ldap = Net::LDAP->new(
    $conf{uri} || 'localhost',
    port => $conf{port} || 389,
    onerror => 'die'
);
$ldap->start_tls if $conf{ssl} eq 'start_tls';
$ldap->bind();
my $result = $ldap->search(
    base => $conf{base},
    scope => $conf{scope} || 'sub',
    filter => "(&(objectClass=posixAccount)(uid=$uid))",
    attrs => ['sshPublicKey']
);
exit unless $result->count;

my @keys = $result->entry(0)->get_value('sshPublicKey');
print join("\n", @keys) . "\n";
